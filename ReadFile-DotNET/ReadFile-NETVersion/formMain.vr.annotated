// ## AVR for .NET program to read a file.
// ---
// Version 1.0
// ---

Using System
Using System.Collections
Using System.ComponentModel
Using System.Data
Using System.Drawing
Using System.Text
Using System.Windows.Forms

BegClass formMain Extends(System.Windows.Forms.Form) Access(*Public) Partial(*Yes)

    /region Default Constructor
    // AVR for .NET's code-behinds are always classes and classes have 
    // constructors. You don't usually put code here. Put set-up code in the 
    // form's Load event. AVR Classic code-behinds don't have constructors. 
    BegConstructor Access(*Public)
        //
        // Required for Windows Form Designer support
        //
        InitializeComponent()

        //
        // TODO: Add any constructor code after InitializeComponent call
        //
    EndConstructor

    /endregion

    // A global variable to govern canceling program.
    DclFld Cancel Type(*Boolean)

    DclDB pgmDB DBName("*Public/DG Net Local") 

    DclDiskFile  CustomerRO +
          Type(*Input) +
          Org(*Indexed) +
          Prefix(Customer_) +
          File("Examples/CMastNewL1") +
          DB(pgmDB) +
          ImpOpen(*No) + 
          RnmFmt(CUSTRO)

    DclDs CustomerEDDS ExtDesc(*Yes) +
                      DBDesc("*PUBLIC/DG NET Local") +
                      FileDesc("Examples/CMastNewL1") +                      
                      Prefix(Customer_) Dim(2)

    // AVR for .NET doesn't support executable code in the mainline;
    // you can only put declarations in AVR for .NET's mainline. You generally
    // put program setup stuff in a form's Load event in AVR for .NET.
    BegSr Form1_Load Access(*Private) Event(*this.Load)
        DclSrParm sender *Object
        DclSrParm e System.EventArgs

        // Occurs when form is first loaded.
        // Put form "startup" code here (ie open files).

        labelCustomerName.Text = String.Empty 
        // AVR for .NET requires you to explicitly connect to the database. 

        Connect pgmDB         
        Open CustomerRO 
    EndSr

    BegSr Form1_FormClosing Access(*Private) Event(*this.FormClosing)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.Windows.Forms.FormClosingEventArgs)

        // Occurs when form is closing.    
        // Put form "housecleaning" code here (ie close files).

        // CloseReason.UserClosing = closing from control box. 

        If buttonCancel.Enabled // AND e.CloseReason = CloseReason.UserClosing
            e.Cancel = *True
        EndIf
    EndSr

    BegSr Form1_FormClosed Access(*Private) Event(*this.FormClosed)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.Windows.Forms.FormClosedEventArgs)

        Close *All 
        Disconnect pgmDB 
    EndSr

    BegSr buttonRead_Click Access(*Private) Event(*this.buttonRead.Click)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        PrepInterface()
        ReadFile()
        PrepInterface()
    EndSr

    BegSr buttonCancel_Click Access(*Private) Event(*this.buttonCancel.Click)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        Cancel = *ON
    EndSr
    
    BegSr buttonExit_Click Access(*Private) Event(*this.buttonExit.Click)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        EndProgram()
    EndSr
    
    BegSr exitToolStripMenuItem_Click Access(*Private) Event(*this.exitToolStripMenuItem.Click)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        EndProgram()
    EndSr
    
    // End of event handlers.        

    BegSr ReadFile
        DclFld TenPercent    Type(*Integer4)
        DclFld RecordCount   Type(*Integer4)

        // Calculate 10% of the records in the file. 
        TenPercent = CustomerRO.RecCount / 10
        RecordCount = 0 

        // Set initial progress bar properties.
        progressbarCustomer.Minimum     =  0
        progressbarCustomer.Maximum     = 10
        progressbarCustomer.Value   =  0  // .NET is zero-based.
        progressbarCustomer.Visible = *On

        *This.Cursor =  System.Windows.Forms.Cursors.AppStarting

        Cancel = *Off
        Occur CustomerEDDS NewIndex(1) 
        SetLL CustomerRO Key(*LoVal) 
        Read CustomerRO 

        DoWhile (NOT CustomerRO.IsEof) AND (NOT Cancel) 
            DoEvents
            // Update progress bar?
            RecordCount = RecordCount + 1
            If ( RecordCount >= TenPercent ) 
                RecordCount  = 0
                progressbarCustomer.Value = progressbarCustomer.Value + 1
            EndIf 
            
            Customer_CMName = Customer_CMName 
            labelCustomerName.Text = Customer_CMName
            Read CustomerRO
        EndDo
        progressbarCustomer.Visible = *Off
        
        *This.Cursor = System.Windows.Forms.Cursors.Default
    EndSr

    BegSr PrepInterface
        buttonRead.Enabled = (NOT buttonRead.Enabled) 
        buttonCancel.Enabled = (NOT buttonCancel.Enabled) 
        buttonExit.Enabled = (NOT buttonExit.Enabled) 
    EndSr      

    BegSr EndProgram
        Application.Exit()
    EndSr
    
EndClass
